setenv bootcmd 'run m_autoboot;'
setenv bootdelay '5'
setenv fdt 'sun50i-a64-pine64-plus.dtb'
setenv fdt_addr_r '0x45000000'
setenv ramdisk 'kernel/uInitrd'
setenv ramdisk_addr_r '0x45300000'
setenv kernel 'Image'
setenv kernel_addr_r '0x41080000'
setenv part '0:2'
setenv dev 'mmc'
setenv prefix '/boot/conf.d'
setenv k_console 'console=tty0 console=ttyS0,115200n8 earlycon=uart,mmio32,0x01c28000'
setenv k_rootfs 'root=/dev/mmcblk0p2 rootwait rw fsck.repair=yes'
setenv k_governor 'governor=ondemand'
setenv k_net 'net.ifnames=0'
setenv k_video 'no_console_suspend'
setenv m_set_bootargs 'setenv bootargs "${k_console} ${k_rootfs} ${k_governor} ${k_net} ${k_video} elevator=noop";'
setenv m_load_kernel 'ext4load ${dev} ${part} ${kernel_addr_r} ${prefix}/${conf}/kernel/${kernel};'
setenv m_load_ramdisk 'ext4load ${dev} ${part} ${ramdisk_addr_r} ${prefix}/${conf}/${ramdisk};'
setenv m_load_fdt 'ext4load ${dev} ${part} ${fdt_addr_r} ${prefix}/${conf}/kernel/${fdt};'
setenv m_boot 'run m_load_kernel; run m_load_ramdisk; run m_load_fdt; run m_set_bootargs; booti ${kernel_addr_r} ${ramdisk_addr_r} ${fdt_addr_r};'
setenv m_autoboot 'setenv conf "default"; run m_boot;'
setenv _t 'setenv conf "test"; run m_boot;'
